buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://www.jetbrains.com/intellij-repository/releases' }
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.0.0'
    id 'org.jetbrains.intellij.platform' version '2.2.1'
}

group 'com.nilsenlabs.flavormatrix'
version '1.4.2'

Properties properties = new Properties()
properties.load(project.rootProject.file("local.properties").newDataInputStream())

// Any developer must change this to his/her installation folder
// of Android Studio of the targetIdeVersion for which to build for
// When updating, delete all source jar references manually from Project Structure > Libraries that reference
// the old version of Android Studio. E.g::
//  plugins\properties\lib\platform-images.jar
//  plugins\properties\lib\properties.jar
//  plugins\smali\lib\smali.jar
def defaultLocalStudioPath = 'd:/coding/android-studio-meerkat'

// Specify the path in your local.properties file as
// localStudioPath=your/path/to/androidstudio
// Also see https://plugins.jetbrains.com/docs/intellij/tools-gradle-intellij-plugin.html#intellij-extension-localpath
def localStudioPath = properties.get("localStudioPath") ?: defaultLocalStudioPath

// Below: Meerkat 2024.3.1 Canary 9. Remember to update plugin.xml
def targetIdeVersion = "243.22562.218"


patchPluginXml {
    changeNotes = """
    <ul>
      <li>1.4.2 Publishing technicalities - no functionality change</li>
      <li>1.4.0 Support for Android Studio Meerkat (from Canary 9)</li>
      <li>1.3.0 Support for Android Studio Ladybug</li>
      <li>1.2.0 Support for Android Studio Giraffe and Hedgehog</li>
      <li>1.1.0 Support for Android Studio Electric Eel</li>
      <li>1.0.0 Support for Android Studio Chipmunk (RC2+)</li>
      <li>0.7.0 Support for Android studio Arctic Fox 2020.3.1</li>
    </ul>
    """
}

repositories {
    mavenLocal()
    mavenCentral()
    intellijPlatform {
        defaultRepositories()
    }
    maven { url 'https://www.jetbrains.com/intellij-repository/releases' }
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation "org.mockito:mockito-core:2.+"
    intellijPlatform {
        local(localStudioPath)
        //androidStudio("$targetIdeVersion") // If you want IntelliJ to download AS. instead of "local"
        bundledPlugin('com.intellij.gradle')
        plugin("org.jetbrains.android:$targetIdeVersion")
    }
}
